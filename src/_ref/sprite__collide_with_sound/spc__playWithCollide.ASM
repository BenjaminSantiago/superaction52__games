;Table Assembler compatable SPC700 program
;Test of SPC based sound effects using WAV2BRR
;(C) 1999 Realtime Simulations and Roleplaying games, Inc
;
; When loaded into the SPC, will play whatever sample is loaded after it
;
; So, on the 65816 side, this chunk of code is loaded into the SPC700 at $400
; The sound sample to be played is loaded into $500
; The sound sample is in BRR format, as specified in the SPC700 documentation
; BRR files can be generated by WAV2BRR or by ripping data from other ROMs
;
; Be sure to pad this to 256 bytes!! 
transit .equ $00

        .org $0400
        ;this is the value that the SNES and SPC 
        ;both look at to figure out what to do

        mov transit,    #$00
        mov $04, #$00
        mov $03, #$00

        ;setup DSP
        ;---------------------------------------------------------------
        ;First order of business is to setup the DSP registers
        ;We'll start with the soundsample directory
        mov A,#$5D      ;the DSP address we want to access
                        ;5D is the directory

        mov $F2,A       ;F2 -->
                        ;where we specify the DSP register to access

        mov A,#$02      ;the data we want to give the register (offset by 100h, so the directory is 200)
        mov $F3,A       ;F3 -->
                        ;the data we want to give the DSP

        ;what are the exclamation points about?
        mov A,#$00
        mov !$0200,A
        mov !$0202,A    ;loop point is also set to $0500, so
        mov A,#$05      ; sample repeats if loop bit in sample is set
        mov !$0201,A
        mov !$0203,A     ;Point directory entry zero to $0500


        ;this is where the sound gets played
        ;---------------------------------------------------------------
        ;Now we'll do a loop that pokes the soundtable into
        ;DSP registers.  When a negative register is hit, the
        ;loop will terminate.  Then, the CPU will halt.
SoundStart
        mov X,#$00
ExecSound
        mov A,!soundtable+X
        bmi EndSound
        mov $F2,A
        inc X
        mov A,!soundtable+X
        mov $F3,A
        inc X 
        bra ExecSound
EndSound
        mov $F4, #$EB
        mov transit, #$EB

        ;MAIN LOOP
        ;---------------------------------------------------------------
MainLoop
        mov A,$F4               
        cmp A, transit             
        beq MainLoop

        ;#$BE is the command to play the sound
        cmp A, #$BE
        bne MainLoop        

        mov $F4, #$BE
        mov $F4, transit
        bra SoundStart

        bra MainLoop      ;Continue infinite loop
        ;---------------------------------------------------------------

        ;The following table is "poked" into the sound DSP using the
        ;"ExecSound" subroutine.
        ;Take care that the table not exceed 256 bytes (maximum X index)
        ;---------------------------------------------------------------
soundtable
        .byte $00
        .byte $7F           ;Left volume (V1) to max
        .byte $01
        .byte $7F           ;Right volume (V1) to max
        .byte $02
        .byte $10
        .byte $03
        .byte $10           ;base pitch

        .byte $04
        .byte $00           ;Sample #0
        .byte $05
        .byte $E8
        .byte $06
        .byte $60           ;ADSR Settings
        .byte $0C
        .byte $7F           ;Main volume left

        .byte $1C
        .byte $7F           ;Main volume right
        .byte $2C
        .byte $00           ;Echo volume left
        .byte $3C
        .byte $00           ;Echo volume right
        .byte $4D
        .byte $00           ;Echo off

        .byte $3D
        .byte $00           ;Noise off
        .byte $2D
        .byte $00           ;Modulation off
        .byte $07
        .byte $7F           ;Gain off
        .byte $6C
        .byte $20           ;Mute off, echo off

        .byte $4C
        .byte $01           ;Key On for voice 1 and voice 2

        .byte $FF           ;Terminator Byte (do not remove!)
endsoundtable  
      
.end